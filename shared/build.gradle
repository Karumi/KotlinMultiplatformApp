import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

group 'com.karumi'
version '0.0.1'

apply plugin: "kotlin-multiplatform"
apply plugin: "kotlinx-serialization"
apply plugin: 'com.android.library'
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'com.squareup.sqldelight'

android {
    defaultConfig {
        compileSdkVersion 28
        minSdkVersion 21
    }
    packagingOptions {
        exclude "META-INF/*"
    }
}

sqldelight {
    GalleryDb {
        packageName = "com.karumi.gallery"
    }
}

kotlin {
    final def iosTarget = System.getenv('SDK_NAME')?.startsWith("iphoneos") ? presets.iosArm64 : presets.iosX64
    targetFromPreset(iosTarget, "ios") {
        binaries {
            framework("Shared")
        }
    }
    android()

    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.Experimental')
                useExperimentalAnnotation('kotlin.time.ExperimentalTime')
                useExperimentalAnnotation('kotlinx.coroutines.FlowPreview')
                useExperimentalAnnotation('kotlinx.coroutines.ExperimentalCoroutinesApi')
            }
        }
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$serialization_version"

                implementation "io.ktor:ktor-client-core:$ktor_version"
                implementation "io.ktor:ktor-client-json:$ktor_version"
                implementation "io.ktor:ktor-client-logging:$ktor_version"
                implementation "io.ktor:ktor-client-serialization:$ktor_version"
                implementation "co.touchlab:stately:0.9.2"
                implementation "com.squareup.sqldelight:runtime:1.2.0"
            }
        }
        commonTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
                implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

                api "io.ktor:ktor-client-mock:$ktor_version"
            }
        }
        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

                implementation "io.ktor:ktor-client-android:$ktor_version"
                implementation "io.ktor:ktor-client-core-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-json-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-logging-jvm:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-jvm:$ktor_version"
            }
        }
        androidTest {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

                implementation "io.ktor:ktor-client-mock-jvm:$ktor_version"
            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"

                implementation "io.ktor:ktor-client-ios:$ktor_version"
                implementation "io.ktor:ktor-client-core-native:$ktor_version"
                implementation "io.ktor:ktor-client-json-native:$ktor_version"
                implementation "io.ktor:ktor-client-logging-native:$ktor_version"
                implementation "io.ktor:ktor-client-serialization-native:$ktor_version"

                implementation "com.squareup.sqldelight:ios-driver:1.2.0"
            }
        }
        iosTest {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:$serialization_version"

                implementation "io.ktor:ktor-client-mock-native:$ktor_version"
            }
        }
    }
}

configurations {
    compileClasspath
}

task kotlinConfiguration(type: Sync) {
    println "Generating kotlin configuration"

    def generatedSources = new File(buildDir, "generated/kotlin/config")
    generatedSources.mkdirs()
    kotlin.sourceSets.commonMain.kotlin.srcDirs += generatedSources

    def localPropertiesFile = project.file("gradle.properties")
    def localProperties = new Properties()
    localProperties.load(new FileInputStream(localPropertiesFile))

    def propertiesVariables = ""
    for (entry in localProperties) {
        def key = entry.key.toUpperCase()
        propertiesVariables += "\n  val $key = $entry.value"
    }

    def kotlinConfig = new File(generatedSources, "KotlinConfig.kt")
    kotlinConfig.text =
            "package com.karumi.gallery.generated\nobject KotlinConfig {$propertiesVariables\n}"

    println "Kotlin configuration done"
}

tasks.build.dependsOn kotlinConfiguration

task copyFramework {
    final File frameworkDir = new File(buildDir, "xcode-frameworks")
    def buildType = project.findProperty('kotlin.build.type') ?: 'DEBUG'
    def framework = kotlin.targets.ios.compilations.main.target.binaries.findFramework("Shared", buildType)
    dependsOn framework.linkTask

    doLast {
        def srcFile = framework.outputFile
        copy {
            from srcFile.parent
            into frameworkDir
            include 'Shared.framework/**'
            include 'Shared.framework.dSYM'
        }
    }
}

ktlint {
    version = "0.36.0"
    verbose = true
    android = false
    outputToConsole = true
    reporters {
        reporter(ReporterType.PLAIN)
        reporter(ReporterType.CHECKSTYLE)
    }
    //Filters are not working properly
    filter {
        //Hotfix https://github.com/JLLeitschuh/ktlint-gradle/issues/266#issuecomment-529527697
        exclude { element -> element.file.path.contains("sqldelight/") }
    }
}

apply from: "gradleconfig/lint.gradle"
